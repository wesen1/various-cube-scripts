//
// @author wesen
// @copyright 2021 wesen <wesen-ac@web.de>
//
// Based on the respawn script by "Mash"
// See: https://forum.cubers.net/thread-38-post-165672.html#pid165672
//

alias nop []

alias fireEvent [
 eventStatement = $arg1
 eventListener = $arg2
 if (! (&& (checkalias arg3) $arg3)) [
   eventStart = (millis)
 ]
 sleep 1 [
   if ($eventStatement) [
     ($eventListener (- (millis) $eventStart))
   ] [
     fireEvent $eventStatement $eventListener 1
   ]
 ]
]

checkinit mapstartalways [
   eventStart = 0
   eventStatement = 0
   eventListener = nop
]

alias isSpectate [
 return (= (curteam (player1 cn)) 4)
]
alias enterTeam [
 team $teamwish
 attack
]
alias respawn [
 if (! (&& (checkalias arg1) $arg1)) [
   if (= (curteam) 0) [
     teamwish = [CLA]
   ]
   if (= (curteam) 1) [
     teamwish = [RVSF]
   ]
 ]
 spectate
 fireEvent isSpectate enterTeam
]
