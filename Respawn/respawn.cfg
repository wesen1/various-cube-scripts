//
// @author wesen
// @copyright 2021 wesen <wesen-ac@web.de>
//
// Based on the respawn script by "Mash"
// See: https://forum.cubers.net/thread-38-post-165672.html#pid165672
//

persistidents 0;


//
// Stores whether the current respawn attempt should be cancelled
//
// @var int doCancelCurrentRespawnAttempt
//
checkinit doCancelCurrentRespawnAttempt 0;


// Function Utils

//
// Creates a new function with non persistent variables inside the function body.
//
// @param string functionName ($arg1) The name of the function
// @param string functionBody ($arg2) The function body
//
const function [
  const $arg1 (format "persistidents 0; %1" $arg2);
]

//
// Creates a new event listener for a event.
//
// @param string eventName ($arg1) The event name
// @param string eventListenerBody ($arg2) The event listener body
//
function eventlistener [
  add2alias $arg1 (format "persistidents 0; %1; persistidents 1" $arg2);
]


// Public Functions

//
// Changes to spectate team and tries to respawn in the same team as before as soon as possible.
//
function respawn [

  if (= (curteam) 0) [
    preferredTeam = "CLA";
  ][
    if (= (curteam) 1) [
      preferredTeam = "RVSF";
    ]
  ]

  spectate;

  doCancelCurrentRespawnAttempt = 0;
  onSpectateCallback = (format "enterTeam %1;" $preferredTeam);
  waitUntilConditionIsFulfilled "isPlayer1Spectating" $onSpectateCallback "doCancelCurrentRespawnAttempt";

]


// Event listeners

//
// Event listener that is called when a new game starts.
// Cancels the current respawn attempt if there is one.
//
eventlistener mapstartalways [
  cancelCurrentRespawnAttempt;
]


// Private Functions

//
// Waits until a given condition is fulfilled and calls a callback.
//
// @param string condition ($arg1) The condition (Either a function name or a variable name)
// @param string callback ($arg2) The callback (Either a function name or a cubescript block to execute)
// @param string cancelCondition ($arg3) The cancel condition (Either a function name or variable name) (optional)
//
function waitUntilConditionIsFulfilled [

  condition = $arg1;
  callback = $arg2;

  if (>= $numargs 3) [
    cancelCondition = $arg3;
  ][
    cancelCondition = 0;
  ]

  if ($condition) [
    execute $callback;
  ][
    if (! $cancelCondition) [
      sleep 1 [
        waitUntilConditionIsFulfilled $condition $callback $cancelCondition;
      ]
    ]
  ]

]

//
// Returns whether player1 is currently spectating.
//
// @return int 1 if player1 is currently spectating, 0 otherwise
//
function isPlayer1Spectating [
  return (= (curteam) 4);
]

//
// Enters a given team.
//
// @param string teamName ($arg1) The "team" compatible team name of the team to join
//
function enterTeam [
  team $arg1;
  attack;
]

//
// Cancels the current respawn attempt.
//
function cancelCurrentRespawnAttempt [
  doCancelCurrentRespawnAttempt = 1;
]


// Documentation
docsection "Respawn";

docident "respawn" "Changes to spectate team and tries to respawn in the same team as before as soon as possible.";
