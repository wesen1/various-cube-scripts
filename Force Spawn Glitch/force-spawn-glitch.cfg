//
// @copyright 2018 wesen
// @author wesen <wesen-ac@web.de>
//

checkinit forceSpawnGlitch 0;
checkinit desiredXMin 0;
checkinit desiredXMax 0;
checkinit desiredYMin 0;
checkinit desiredYMax 0;
checkinit desiredZMin 0;
checkinit desiredZMax 0;

//
// De-/Activates the force spawn glitch mode.
//
// @param bool $arg1 The new spawn glitch mode state
//
const setForceSpawnGlitchMode [

  forceSpawnGlitch = $arg1;

]

//
// Event handler that is called when player1 or a bot spawns.
//
checkinit onSpawn [

  if (&& $forceSpawnGlitch (! (isInDesiredRange))) [
    respawn;
  ]

]

//
// Returns whether the player position is in the desired range
//
// @return int 0: Position is not in desired range
//             1: Position is in desired range
//
const isInDesiredRange [

  if (|| (< (player1 x) $desiredXMin) (> (player1 x) $desiredXMax)) [
    return 0;
  ][
    if (|| (< (player1 y) $desiredYMin) (> (player1 y) $desiredYMax)) [
      return 0;
    ][

      if (|| (< (player1 z) $desiredZMin) (> (player1 z) $desiredZMax)) [
        return 0;
      ][
        return 1;
      ]
    ]
  ]

]

//
// Sets one corner of the square in which you wish to spawn.
//
const setPoint [

  if (> (player1 x) $desiredXMin) [
    desiredXMin = $desiredXMax;
    desiredXMax = (player1 x);
  ][
    desiredXMax = $desiredXMin;
    desiredXMin = (player1 x);
  ]

  if (> (player1 y) $desiredYMin) [
    desiredYMin = $desiredYMax;
    desiredYMax = (player1 y);
  ][
    desiredYMax = $desiredYMin;
    desiredYMin = (player1 y);
  ]

  if (> (player1 z) $desiredZMin) [
    desiredZMin = $desiredZMax;
    desiredZMax = (player1 z);
  ][
    desiredZMax = $desiredZMin;
    desiredZMin = (player1 z);
  ]

]

