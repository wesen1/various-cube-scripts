//
// @author wesen
// @copyright 2018 wesen <wesen-ac@web.de>
//

checkinit spamChatActive 0;

// Characters that will be used to generate words 
const chatCharacters "A B C D E F G H I J K L M N O P Q R S T U V W X Y Z a b c d e f g h i j k l m n o p q r s t u v w x y z 1 2 3 4 5 6 7 8 9 0"

// All characters that can be sent by ac client (except for '"')
const allChatCharacters "A B C D E F G H I J K L M N O P Q R S T U V W X Y Z a b c d e f g h i j k l m n o p q r s t u v w x y z 1 2 3 4 5 6 7 8 9 0 < , . - # + ^ > ; : _ ' * ! $ % & / ( ) = ? ` | ~ \ } ] [ { @"

//
// Returns a random character from the list of chat characters.
//
// @return String The random character
//
const getRandomCharacter [

  return (at $chatCharacters (rnd (listlen $chatCharacters)));

]

//
// Generates and returns a random word with between 4 and 7 random letters.
//
// @return String The random word
//
const generateRandomWord [

  wordLength = (+ (rnd 7) 4);
  randomWord = "";

  loop i $wordLength [

    randomWord = (concatword $randomWord (getRandomCharacter));

  ]

  return $randomWord;

]

const maximumSentenceLength 254;

//
// Generates and returns a sentence consisting of random words.
//
const generateRandomSentence [

  amountWords = (+ (rnd 20) 1);
  randomSentence = "";

  loop i $amountWords [

    randomWord = (generateRandomWord)

    if (< (+ (strlen $randomSentence) (strlen $randomWord)) $maximumSentenceLength) [
      randomSentence = (concat $randomSentence $randomWord);

    ][
      break;
    ]

  ]

  return $randomSentence;

]


//
// De-/Activates the spam chat.
//
// @param int $arg1 The new state of the spam chat mode
//
const setSpamChatActive [

  spamChatActive = $arg1;

]

//
// Starts an infinite loop of chat spam.
// This will only work when the spam chat mode was enabled before by using /setSpamChatActive 1.
//
const spamChat [

  if ($spamChatActive) [

    say (generateRandomSentence);
    sleep (+ (rnd 500) 100) [
      spamChat;
    ]

  ]

]

// Documentation
docsection "Spam: Chat";

docident "chatCharacters" "Stores the characters that will be used to generate random words";
docident "allChatCharacters" "Stores all characters that can be sent to other clients via chat";
docident "maximumSentenceLength" "Stores the maximum amount of characters that a random sentence may have";

docident "getRandomCharacter" "Returns a random character from the list of chat characters";
docident "generateRandomWord" "Generates and returns a random word with between 4 and 7 random letters";
docident "generateRandomSentence" "Generates and returns a sentence consisting of random words";

docident "setSpamChatActive" "De-/Activates the spam chat";
docargument "A" "The new state of the spam chat mode";
docexample "setSpamChatActive 1" "Activates the spam chat mode";

docident "spamChat" "Starts an infinite loop of chat spam";
docremark "This will only work when the spam chat mode was enabled before by using /setSpamChatActive 1";

