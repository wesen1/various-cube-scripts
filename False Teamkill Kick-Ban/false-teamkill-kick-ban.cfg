//
// @author wesen
// @copyright 2018 wesen <wesen-ac@web.de>
//

persistidents 0;

//
// Calls a vote to kick a player for the reason 'teamkill' with a false amount of teamkills.
//
// @param int $arg1 The target cn
// @param int $arg2 The fake amount of teamkills
//
const kickTeamKill [

  targetCn = $arg1;
  fakeTeamKills = $arg2;

  kick $targetCn (generateReason $targetCn $fakeTeamKills);

]

//
// Calls a vote to ban a player for the reason 'teamkill' with a false amount of teamkills.
//
// @param int $arg1 The target cn
// @param int $arg2 The fake amount of teamkills
//
const banTeamKill [

  targetCn = $arg1;
  fakeTeamKills = $arg2;

  ban $targetCn (generateReason $targetCn $fakeTeamKills);

]

//
// Generates and returns a kick/ban reason with a fake amount of teamkills.
//
// @param int $arg1 The target cn
// @param int $arg2 The fake amount of teamkills
//
// @return String The kick/ban reason
//
const generateReason [

  targetCn = $arg1;
  fakeTeamKills = $arg2;

  // Get the current player ping to make the vote look more real
  targetPing = (player $targetCn ping);

  // Generate the vote reason
  voteReason = (concatword "teamkill (" $fakeTeamKills " tks, ping " $targetPing ")");

  // Push the real (x tks, ping x) field out of the screen by adding empty space after your fake statistics
  loop i 1000 [
    voteReason = (concatword $voteReason " ");
  ]

  return $voteReason;

]


// Documentation
docsection "False Teamkill Kick and Ban";

docident "kickTeamKill" "Calls a vote to kick a player for the reason 'teamkill' with a false amount of teamkills";
docargument "cn" "The target cn" "0...16" 1;
docargument "tks" "The fake amount of teamkills" "" 1;
docexample "kickTeamKill 0 10" "Calls a kick vote with the target <player 0> and the reason 'teamkill (10 tks, ping <player 0 ping>)'";

docident "banTeamKill" "Calls a vote to ban a player for the reason 'teamkill' with a false amount of teamkills";
docargument "cn" "The target cn" "0...16" 1;
docargument "tks" "The fake amount of teamkills" "" 1;
docexample "banTeamKill 0 10" "Calls a ban vote with the target <player 0> and the reason 'teamkill (10 tks, ping <player 0 ping>)'";

docident "generateReason" "Generates and returns a kick/ban reason with a fake amount of teamkills";
docargument "cn" "The target cn" "0...16" 1;
docargument "tks" "The fake amount of teamkills" "" 1;

