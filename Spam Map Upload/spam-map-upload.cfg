//
// @author wesen
// @copyright 2018 wesen <wesen-ac@web.de>
//

persistidents 0;

// Characters that may be used in a map name
const mapNameCharacters "A B C D E F G H I J K L M N O P Q R S T U V W X Y Z a b c d e f g h i j k l m n o p q r s t u v w x y z 1 2 3 4 5 6 7 8 9 0 - _";

// List of generated maps
mapNameList = "";


//
// Generates and returns a random map name.
//
// @return String The random map name
//
const generateMapName [

  mapName = ""

  // Chooses a random number between 0 and 63 and adds 1 to it
  // The maximum map name length is 64 characters
  mapNameLength = (+ (rnd 63) 1);

  loop i $mapNameLength [

    // Select a random character
    randomCharacter = (at $mapNameCharacters (rnd (listlen $mapNameCharacters)));

    // Add the character to mapname
    mapName = (concatword $mapName $randomCharacter);

  ]

  return $mapName;

]

//
// Saves a map a variable amount of times with random names.
//
// @param $arg1 The amount of maps that will be generated
// @param $arg2 The amount of already generated maps
//
const mapSpamSaveMap [

  counter = 0;
  amountMaps = $arg1;

  if (! $arg2) [

    // Reset the map name list
    mapNameList = "";

  ][
    counter = $arg2;
  ]

  if (< $counter $amountMaps) [

    randomMapName = (generateMapName);

    // Check whether map name already exists in map name list
    if (! (strstr $mapNameList $randomMapName)) [

      ++ counter;

      mapNameList = (concat $mapNameList $randomMapName);
      savemap $randomMapName;

    ]

    sleep (* $i 5) [
      mapSpamSaveMap $amountMaps $counter;
    ]

  ]

]

//
// Uploads all maps that were previously saved with mapSpamSaveMap.
//
const mapSpamUploadMaps [

  looplist $mapNameList mapName [
    sendmap $mapName;
  ]

]


// Documentation
docsection "Spam: Map Upload";

docident "mapNameCharacters" "Stores the characters that may be used in a map name";

docident "generateMapName" "Generates and returns a random map name";

docident "mapSpamSaveMap" "Saves a map a variable amount of times with random names";
docargument "A" "The amount of maps that will be generated" "" 1;
docargument "B" "The amount of already generated maps (optional)" "" 1;
docexample "mapSpamSaveMap 10" "Saves the current map 10 times with random names";

docident "mapSpamUploadMaps" "Uploads all maps that were previously saved with mapSpamSaveMap";

